model
{
    for (j in 1:N) {
        timen[j] <- (time[j])
        timen1[j] <- (equals(P1, 0.00000E+00) * log(timen[j]) + 
            (1 - equals(P1, 0.00000E+00)) * pow(timen[j], P1))
        r[j] ~ dbin(p[j], z[j])
        p[j] <- 1 - exp(-h[j] * dt[j])

	# Deviance calculation
    	rhat[j] <- p[j] * z[j]
    	
	#Deviance contribution
    	dev[j] <- 2 * (r[j] * (log(r[j]+0.01)-log(rhat[j] + 0.01))
                     +  (z[j]-r[j]) * (log(z[j]-r[j]+0.01) - log(z[j]-rhat[j] + 0.01)))
        
	# Fixed effects model
	# loop over datapoints
        log(h[j]) <- Alpha[s[j], a[j], 1] + Alpha[s[j], a[j], 
            2] * timen1[j]
    }
    for (i in 1:ns) {
        w[i, 1] <- 0.00000E+00
        delta[i, 1] <- 0.00000E+00
        for (k in 1:na[i]) {
            Alpha[i, k, 1] <- mu[i, 1] + delta[i, k]
            Alpha[i, k, 2] <- mu[i, 2] + d[t[i, k], 2] - d[t[i, 
                1], 2]
        }
        for (k in 2:na[i]) {
            delta[i, k] ~ dnorm(md[i, k], taud[i, k])
            md[i, k] <- d[t[i, k], 1] - d[t[i, 1], 1] + sw[i, 
                k]
            w[i, k] <- (delta[i, k] - d[t[i, k], 1] + d[t[i, 
                1], 1])
            sw[i, k] <- sum(w[i, 1:k - 1])/(k - 1)
            taud[i, k] <- tau * 2 * (k - 1)/k
        }
    }


	# Total residual deviance
	  totresdev <- sum(dev[])

    for (i in 1:ns) {
        mu[i, 1:2] ~ dmnorm(mean[1:2], prec[, ])
    }
    d[1, 1] <- 0.00000E+00
    d[1, 2] <- 0.00000E+00
    for (k in 2:nt) {
        d[k, 1:2] ~ dmnorm(mean[1:2], prec[, ])
    }
    sd ~ dunif(0.00000E+00, 2)
    tau <- 1/(sd * sd)
    for (m in 1:maxt) {
        time1[m] <- (equals(P1, 0.00000E+00) * log(m) + (1 - equals(P1, 
            0.00000E+00)) * pow(m, P1))
    }
    for (c in 1:(nt - 1)) {
        for (k in (c + 1):nt) {
            for (m in 1:maxt) {
                log(HR[c, k, m]) <- (d[k, 1] - d[c, 1]) + (d[k, 
                  2] - d[c, 2]) * time1[m]
            }
        }
    }
    for (k in 1:nt) {
        alpha0[k] <- mu[8, 1] + d[k, 1]
        alpha1[k] <- mu[8, 2] + d[k, 2]
        for (m in 1:maxt) {
            log(HAZARD[k, m]) <- alpha0[k] + alpha1[k] * time1[m]
            CUM_H[k, m] <- sum(HAZARD[k, 1:m])
            T[k, m] <- 1 - exp(-CUM_H[k, m])
            S[k, m] <- 1 - T[k, m]
        }
    }
}
